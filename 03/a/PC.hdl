// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Increment the previous value
    Inc16 (in=prev, out=incPrev);
    // Do we want the incremented version or the non-incremented version?
    Mux16 (a=prev, b=incPrev, sel=inc, out=incMux);
    // Do we want to load the incremented/non-incremented version of the
    // previous value or do we want to load the new input?
    Mux16 (a=incMux, b=in, sel=load, out=loadMux);
    // Do we want to load the input/previous/incPrev value or reset the
    // Register to 0?
    Mux16 (a=loadMux, b=false, sel=reset, out=final);
    // Load the final input to the register
    Register (in=final, load=true, out=prev, out=out);
}
